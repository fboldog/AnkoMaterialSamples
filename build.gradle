buildscript {
    ext.travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    ext.preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

    ext.kotlinVersion = '1.1.1'
    ext.ankoVersion = '0.10.0-beta-1'
    ext.supportVersion = '25.2.0'

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId 'com.ferencboldog.ankomaterial'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName '1.0'
    }

    dexOptions {
        if (travisBuild) {
            preDexLibraries = preDexEnabled && !travisBuild
        } else {
            preDexLibraries = true
        }
    }

//     testOptions {
//         unitTests.returnDefaultValues = true
//     }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    def support = ['appcompat-v7', 'design', 'support-v4', 'recyclerview-v7', 'percent', 'cardview-v7']
    support.each { name ->
        implementation "com.android.support:$name:$supportVersion"
    }

    def anko = ['sdk15', 'support-v4', 'design', 'appcompat-v7', 'recyclerview-v7', 'percent', 'cardview-v7']

    anko.each { name ->
        implementation "org.jetbrains.anko:anko-$name:$ankoVersion"
    }

    // testCompile 'junit:junit:4.12'
    // testCompile 'org.assertj:assertj-core:2.4.0'
    // testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}

task wrapper(type: Wrapper) {
    distributionUrl = 'https://services.gradle.org/distributions/gradle-4.7-all.zip'
}
